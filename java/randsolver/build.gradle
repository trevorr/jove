plugins {
    id 'com.intershop.gradle.javacc' version '3.1.1'
    id 'java-library'
}
repositories {
   mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}
dependencies {
    implementation project(":langschema-jove")
    implementation project(":langschema-java")
    implementation project(":langschema")
    implementation project(":jove")
//    testImplementation "junit:junit:4.12"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testCompileOnly 'junit:junit:4.13'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'

}
tasks.withType(JavaCompile) {
//    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
    options.deprecation = true
}
javacc {
    // configuration container for all javacc configurations
    configs {
        template {
            javaCCVersion = '3.2'
            inputFile = file('src/com/newisys/randsolver/parser/ConstraintParser.jj')
            packageName = 'com.newisys.randsolver'
            // lookahead = '2'
        }
    }
}

tasks.getByName("javaccTemplate") {
    // The javacc 3.0 does not create the directory it needs
    doFirst {
        mkdir "${buildDir}/generated/javacc/template/com/newisys/randsolver"
    }
    // The javacc 3.0 creates an incorrect Token file, the project provides its own.
    doLast {
        delete "${buildDir}/generated/javacc/template/com/newisys/randsolver/Token.java"
    }
}

test {
    useJUnitPlatform()
}
